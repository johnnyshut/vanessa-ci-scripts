
#Использовать fs
#Использовать yaml

#Область ОписаниеПеременных

Перем Лог;                      // Объект записи лога приложения
Перем РабочийКаталог;           // Строка - текущий рабочий каталог
Перем КаталогПриложения;        // Строка - текущий каталог приложения

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Выполняет логику команды
// 
// Параметры:
//  ПараметрыКоманды  - Структура - Соответствие ключей командной строки и их значений
//
Процедура ВыполнитьКоманду(Знач ПараметрыКоманды) Экспорт
	
	Лог = ПараметрыПриложения.Лог();

	Если Общие.ПроверитьЧтоЭтоКаталогИсходногоПроекта() Тогда
		Возврат; 
	КонецЕсли;
	
	ПутьКСкриптуУстановкиOScript        = ОбъединитьПути(КаталогПриложения, "tools", "scripts", "install-oscript-local.bat");
	ЦелевойПутьКСкриптуУстановкиOScript = ОбъединитьПути(РабочийКаталог, "tools", "scripts", "install-oscript-local.bat");
	ФС.ОбеспечитьКаталог(ОбъединитьПути(РабочийКаталог, "tools", "scripts"));

	ПутьКФайлуШаблонаCI = ОбъединитьПути(КаталогПриложения, "tools", "CI", ".gitlab-ci.yml");
	ПутьКФайлуCI 		= ОбъединитьПути(РабочийКаталог, ".gitlab-ci.yml");

	ЧтениеТекста = Новый ЧтениеТекста(ПутьКФайлуШаблонаCI, КодировкаТекста.UTF8);
	ТекстФайла = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();

	ПарсерYAML = Новый ПарсерYAML;
	РезультатЧтения = ПарсерYAML.ПрочитатьYaml(ТекстФайла);

	МассивШагов = РезультатЧтения.Получить("stages");
	Если МассивШагов = Неопределено Тогда
		ТекстИсключения = СтрШаблон("Ошибка чтения файла шаблона pipeline по пути: %1", ПутьКФайлуШаблонаCI);
		Лог.Ошибка(ТекстИсключения);
		Возврат;
	КонецЕсли;

	КопироватьФайл(ПутьКФайлуШаблонаCI, ПутьКФайлуCI);
	ТекстСообщения = СтрШаблон("Файл настроек pipeline сохранен в %1", ПутьКФайлуCI);
	Лог.Информация(ТекстСообщения);

	КопироватьФайл(ПутьКСкриптуУстановкиOScript, ЦелевойПутьКСкриптуУстановкиOScript);
	ТекстСообщения = СтрШаблон("Скрипт установки OneScript сохранен в %1", ЦелевойПутьКСкриптуУстановкиOScript);
	Лог.Информация(ТекстСообщения);

	Лог.Информация("Инициализация завершена");

КонецПроцедуры // ВыполнитьКоманду

#КонецОбласти

РабочийКаталог = ПараметрыПриложения.ТекущийКаталог();
КаталогПриложения = ПараметрыПриложения.КаталогПриложения();
