#Использовать 1commands 
#Использовать 1connector
#Использовать fs
#Использовать tempfiles

#Область ОписаниеПеременных

Перем Лог;                      // Объект записи лога приложения
Перем КаталогПриложения;        // Строка - текущий каталог приложения

#КонецОбласти

#Область ПрограммныйИнтерфейс

Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	Лог = ПараметрыПриложения.Лог();
	ЛокальныеВременныеФайлы = Новый МенеджерВременныхФайлов();

	// Определяем путь к записи ovm.exe
	
	КаталогПользовательскихПриложений = "%USERPROFILE%\AppData\Local\Microsoft\WindowsApps";
	Если ФС.КаталогСуществует(КаталогПользовательскихПриложений) Тогда
		ПутьOVM = ОбъединитьПути("%USERPROFILE%\AppData\Local\Microsoft\WindowsApps", "ovm.exe");
	Иначе
		ПутьOVM = ЛокальныеВременныеФайлы.НовоеИмяФайла(".exe");
	КонецЕсли;

	// Скачиваем OneScript Version Manager

	// TODO перенести значение репозитория ovm в env.json
	РезультатЗапроса = КоннекторHTTP.Get("https://github.com/oscript-library/ovm/releases/download/v1.2.1/ovm.exe"); 
	Если РезультатЗапроса.КодСостояния = 200 Тогда
		ДвоичныеДанные = РезультатЗапроса.ДвоичныеДанные();
		ДвоичныеДанные.Записать(ПутьOVM);
	Иначе
		ВызватьИсключение "Не удалось скачать ovm.exe";
	КонецЕсли;

	// Устанавливаем OScript
	// ovm: устанавливаем stable версию

	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить("use");
	МассивПараметров.Добавить("--install stable"); // TODO перенести значение stable в env.json

	Общие.ВыполнитьСтороннююКоманду(ПутьOVM, МассивПараметров);

	// Добавляем файл со списком хабов oscript
	// tools\CI\json\opm.cfg -->
	// ..\OneScript\lib\opm\src\cmd\opm.cfg. Лучше для windows: %USERPROFILE%\opm.cfg

	ПутьКФайлуШаблонаНастроекOPM = ОбъединитьПути(КаталогПриложения, "tools", "CI", "json\opm.cfg");
	КопироватьФайл(ПутьКФайлуШаблонаНастроекOPM, "%USERPROFILE%\opm.cfg");

	// Обновляем каталог "oscript_module"
	// opm: устанавливаем локальные зависимости

	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить("install");
	МассивПараметров.Добавить("-l");

	Общие.ВыполнитьСтороннююКоманду("opm", МассивПараметров);

	// Удаляем временные файлы
	ЛокальныеВременныеФайлы.Удалить();

	Лог.Информация("Подготовка завершена");
	
КонецПроцедуры // ВыполнитьКоманду

#КонецОбласти

КаталогПриложения = ПараметрыПриложения.КаталогПриложения();
